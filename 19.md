## Lesson 19

##### Execute Command

In this lesson we replace the currently running process with a new process that executes a command.

---
## Lesson 19

#### Execute Command


##### Firstly, some background

The EXEC family of functions replace the currently running process with a new process, that executes the command you specified when calling it.  We will be using the SYS_EXECVE function in this lesson to replace our program's running process with a new process that will execute the linux program /bin/echo to print out “Hello World!”.

##### Naming convention

The naming convention used for this family of functions is **exec** (execute) followed by one or more of the following letters.

* e - An array of pointers to environment variables is explicitly passed to the new process image.
* l - Command-line arguments are passed individually to the function.
* p - Uses the PATH environment variable to find the file named in the path argument to be executed.
* v - Command-line arguments are passed to the function as an array of pointers.

##### Writing our program

The V & E at the end of our function name means we will need to pass our arguments in the following format:  The first argument is a string containing the command to execute, then an array of arguments to pass to that command and then another array of environment variables that the new process will use.  As we are calling a simple command we won't pass any special environment variables to the new process and instead will pass 0h (null).

Both the command arguments and the environment arguments need to be passed as an array of pointers (addresses to memory).  That's why we define our strings first and then define a simple null-terminated struct (array) of the variables names.  This is then passed to SYS_EXECVE. We call the function and the process is replaced by our command and output is returned to the terminal.


execute.asm
```asm
; Execute
; Compile with: nasm -f elf execute.asm
; Link with (64 bit systems require elf_i386 option): ld -m elf_i386 execute.o -o execute
; Run with: ./execute

%include        'functions.asm'

SECTION .data
command         db      '/bin/echo', 0h     ; command to execute
arg1            db      'Hello World!', 0h
arguments       dd      command
                dd      arg1                ; arguments to pass to commandline (in this case just one)
                dd      0h                  ; end the struct
environment     dd      0h                  ; arguments to pass as environment variables (inthis case none) end the struct

SECTION .text
global  _start

_start:

    mov     edx, environment    ; address of environment variables
    mov     ecx, arguments      ; address of the arguments to pass to the commandline
    mov     ebx, command        ; address of the file to execute
    mov     eax, 11             ; invoke SYS_EXECVE (kernel opcode 11)
    int     80h

    call    quit                ; call our quit function
```

```
~$ nasm -f elf execute.asm
~$ ld -m elf_i386 execute.o -o execute
~$ ./execute
Hello World!
```

Note:
Here are a couple other commands to try.

execute.asm
```asm
SECTION .data
command         db      '/bin/ls', 0h       ; command to execute
arg1            db      '-l', 0h
```

execute.asm
```asm
SECTION .data
command         db      '/bin/sleep', 0h    ; command to execute
arg1            db      '5', 0h
```
